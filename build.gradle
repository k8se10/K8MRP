plugins {
  id 'fabric-loom' version '1.6.+'
  id 'maven-publish'
}

group = project.maven_group
version = project.mod_version

java {
  toolchain { languageVersion = JavaLanguageVersion.of(17) }
  withSourcesJar()
}

repositories {
  maven { url = 'https://maven.fabricmc.net/' }
  mavenCentral()
  maven { url = 'https://maven.quiltmc.org/repository/release/' }
  maven { url = 'https://maven.terraformersmc.com/releases/' }
  maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
  def mc = project.minecraft_version
  def yarn = project.yarn_mappings
  def loader = project.loader_version
  def fapi = project.fabric_api_version

  minecraft "com.mojang:minecraft:${mc}"
  mappings "net.fabricmc:yarn:${yarn}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader}"
  modImplementation "net.fabricmc.fabric-api:fabric-api:${fapi}"
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.release = 17
}

processResources {
  inputs.property 'version', project.version
  filesMatching('fabric.mod.json') {
    expand 'version': project.version, 'minecraft_version': project.minecraft_version
  }
}

jar {
  from("LICENSE") { rename { "${it}_${project.name}" } }
}
